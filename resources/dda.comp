#version 450

struct Rect {
    ivec2 offset;
    uvec2 extent;
};

layout(local_size_x = 8, local_size_y = 8) in;

layout(push_constant) uniform PushConstant {
    float time;
} push;

layout(binding = 0) readonly uniform UniformBuffer {
    Rect output_region;
    Rect display_region;
} uniforms;

layout(binding = 1, rgba8) restrict writeonly uniform image2D render_target;

layout(binding = 2) uniform sampler3D model;

const int STEPS = 2500;

vec3 ray(vec3 dir, vec3 up, vec2 uv) {
    uv -= 0.5;
    uv.y *= float(uniforms.display_region.extent.y) / float(uniforms.display_region.extent.x);

    vec3 right = normalize(cross(up, dir));
    up = normalize(cross(right, dir));

    return normalize(uv.x * right + uv.y * up + dir);
}

vec3 get_voxel(ivec3 p) {
    return texelFetch(model, p, 0).rgb * 0.005;
}

vec3 trace(vec3 ro, vec3 rd, float tfar) {
    ivec3 map_pos = ivec3(floor(ro));
    vec3 delta_dist = abs(1. / rd);
    vec3 sgn = sign(rd);
    ivec3 rs = ivec3(sgn);
    vec3 side_dist = (sgn * (floor(ro) - ro) + (sgn * 0.5) + 0.5) * delta_dist;
    vec3 total = vec3(0);

    float t = 0;

    for (int i = 0; i < STEPS; ++i) {
        total += get_voxel(map_pos);
        bvec3 mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));
        vec3 fmask = vec3(mask);
        side_dist += fmask * delta_dist;
        map_pos += ivec3(mask) * rs;

        if (any(greaterThanEqual(total, vec3(1)))) {
            break;
        }

        if (map_pos.x < 0 || map_pos.y < 0 || map_pos.z < 0) {
            break;
        } else if (map_pos.x > 2048 || map_pos.y > 2048 || map_pos.z > 48) {
            break;
        }
    }

    return total;
}

vec2 aabb_intersect(vec3 bmin, vec3 bmax, vec3 ro, vec3 rd) {
    vec3 tbot = (bmin - ro) / rd;
    vec3 ttop = (bmax - ro) / rd;
    vec3 tmin = min(ttop, tbot);
    vec3 tmax = max(ttop, tbot);
    vec2 t = max(tmin.xx, tmin.yz);
    float t0 = max(t.x, t.y);
    t = min(tmax.xx, tmax.yz);
    float t1 = min(t.x, t.y);
    return vec2(t0, t1);
}

void main() {
    uvec2 index = gl_GlobalInvocationID.xy;

    if (any(greaterThanEqual(index, uniforms.output_region.extent))) {
        return;
    }

    ivec2 pixel = uniforms.output_region.offset + ivec2(index);
    vec2 uv = vec2(pixel - uniforms.display_region.offset) / vec2(uniforms.display_region.extent);

    float t = push.time * 1.2;
    vec3 center = vec3(1024, 1024, 24);
    vec3 ro = center + vec3(sin(t), 0, cos(t)) * 3000.0;
    vec3 rd = normalize(vec3(1024, 1024, 24) - ro);
    rd = ray(rd, vec3(0, 1, 0), uv);

    vec2 hit = aabb_intersect(vec3(0, 0, 0), vec3(2048, 2048, 48), ro, rd);

    vec3 color;
    if (hit.y > max(hit.x, 0.0)) {
        color = trace(ro + rd * hit.x, rd, hit.y);
    } else {
        color = vec3(0, 0, 0);
    }

    imageStore(render_target, ivec2(index), vec4(color, 1));
}
