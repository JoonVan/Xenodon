#version 450

#include "common.glsl"

// Implementation of 'A Fast Voxel Traversal Algorithm for Ray Tracing' by Amanatides & Woo

layout(binding = 2) uniform sampler3D model;

const int STEPS = 7000;

vec3 get_voxel(ivec3 p) {
    return texelFetch(model, p, 0).rgb * uniforms.params.density;
}

vec3 select(vec3 x, bvec3 c) {
    return mix(vec3(0), x, c);
}

vec3 trace(vec3 ro, vec3 rd) {
    vec3 rrd = 1.0 / rd;
    vec3 bias = rrd * ro;

    // Intersect the bounding cube
    float side = max_elem(vec3(textureSize(model, 0)));
    float half_voxel_size = 0.5 / side;

    vec3 box_min = -bias;
    vec3 box_max = uniforms.params.model_dim.xyz * rrd - bias;

    float t_min = max_elem(min(box_min, box_max));
    float t_max = min_elem(max(box_min, box_max));

    if (t_min > t_max) {
        // Ray misses bounding cube
        return vec3(0);
    }

    float density = voxel_density(rd) / side;

    t_min = max(t_min, 0);

    ivec3 end = ivec3(ro + rd * t_max);
    ro += rd * t_min;
    ivec3 pos = ivec3(ro);

    vec3 t_delta = abs(rrd);
    vec3 sgn = sign(rd);
    ivec3 step = ivec3(sgn);

    vec3 side_dist = (sgn * (floor(ro) - ro + 0.5) + 0.5) * t_delta;

    float t = min_elem(side_dist);

    vec3 total = vec3(0);
    int steps = abs(pos.x - end.x) + abs(pos.y - end.y) + abs(pos.z - end.z);
    for (int i = 0; i < steps; ++i) {
        bvec3 mask = lessThanEqual(side_dist.xyz, min(side_dist.yzx, side_dist.zxy));

        side_dist += select(t_delta, mask);
        float t0 = min_elem(side_dist);
        total += texelFetch(model, pos, 0).rgb * (t0 - t) * density;
        t = t0;

        pos += mix(ivec3(0), step, mask);
    }

    return total;
}

void main() {
    uvec2 index = gl_GlobalInvocationID.xy;

    if (any(greaterThanEqual(index, uniforms.output_region.extent))) {
        return;
    }

    ivec2 pixel = uniforms.output_region.offset + ivec2(index);
    vec2 uv = vec2(pixel - uniforms.display_region.offset) / vec2(uniforms.display_region.extent);

    float side = max_elem(vec3(textureSize(model, 0)));
    vec3 ro = push.camera.translation.xyz * side;
    vec3 rd = ray(uv);

    vec3 color = trace(ro, rd);
    imageStore(render_target, ivec2(index), vec4(color, 1));
}
